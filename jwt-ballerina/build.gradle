/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.taskdefs.condition.Os

description = 'Ballerina - JWT Ballerina Generator'

configurations {
    balTools
    jwtBallerinaBirDir
    jwtBallerinaJar
}

dependencies {
    balTools ("org.ballerinalang:jballerina-tools:${ballerinaLangVersion}") {
        transitive = false
    }
}

clean {
    delete "$project.projectDir/target"
    delete "$project.projectDir/Ballerina.lock"
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

task unzip {
    doLast {
        configurations.balTools.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            copy {
                from project.zipTree(artifact.getFile())
                into new File(project.buildDir, "extracted-distribution/")
            }
        }
    }
}

def ballerinaConfigFile = new File("$project.projectDir/Ballerina.toml")
def balProjectVersion = ballerinaConfigFile.text.split("\n")[2].split("=")[1].split("\"")[1]
def artifactJwtBallerinaBirDir = file("$project.projectDir/target/caches/bir_cache/")
def artifactJwtBallerinaJar = file("$project.projectDir/target/caches/jar_cache/ballerina/jwt/${balProjectVersion}/ballerina-jwt-${balProjectVersion}.jar")
def artifactJwtBallerinaDocs = file("$project.projectDir/build/apidocs/")
def artifactJWTBallerinaBalos = file("$project.projectDir/build/balo_cache/")

task ballerinaBuild {
    dependsOn unzip
    inputs.dir file(project.projectDir)
    doLast {
        def distributionBinPath =  project.projectDir.absolutePath + "/build/extracted-distribution/jballerina-tools-${ballerinaLangVersion}/bin"
        exec {
            workingDir project.projectDir
            environment "JAVA_OPTS", "-DBALLERINA_DEV_COMPILE_BALLERINA_ORG=true"
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "$distributionBinPath/ballerina.bat build --code-coverage -c -a"
            } else {
                commandLine 'sh', '-c', "$distributionBinPath/ballerina build --code-coverage -c -a"
            }
        }
        exec {
            workingDir project.projectDir
            environment "JAVA_OPTS", "-DBALLERINA_DEV_COMPILE_BALLERINA_ORG=true"
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "$distributionBinPath/ballerina.bat doc -a"
            } else {
                commandLine 'sh', '-c', "$distributionBinPath/ballerina doc -a "
            }
        }
        copy {
            // Copy the TOML to the same folder as the BIR
            from file("$project.projectDir/Ballerina.toml")
            into file("${artifactJwtBallerinaBirDir}/ballerina/jwt/${balProjectVersion}")
        }

        copy {
            from file("$project.projectDir/target/apidocs/jwt")
            into file("$project.projectDir/build/apidocs/docs/jwt")
        }

        copy {
            from file("$project.projectDir/target/balo/")
            into file("$project.projectDir/build/balo_cache/balo")
        }
    }
    outputs.dir artifactJWTBallerinaBalos
    outputs.dir artifactJwtBallerinaDocs
    outputs.dir artifactJwtBallerinaBirDir
    outputs.file artifactJwtBallerinaJar
}

artifacts {

    jwtBallerinaBirDir file: artifactJwtBallerinaBirDir, builtBy: ballerinaBuild
    jwtBallerinaJar file: artifactJwtBallerinaJar, builtBy: ballerinaBuild

}

task createArtifactZip(type: Zip) {
    destinationDirectory = file("$buildDir/distributions")
    from ballerinaBuild
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact source: createArtifactZip, extension: 'zip'
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ballerina-platform/module-ballerina-jwt")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

build.dependsOn ballerinaBuild
